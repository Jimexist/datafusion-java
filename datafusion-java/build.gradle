plugins {
    id 'java-library'
    id 'maven-publish'
    id 'datafusion.java-conventions'
    id 'com.diffplug.spotless'
    id 'com.google.osdetector'
}

spotless {
    java {
        googleJavaFormat()
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    compileJava {
        options.compilerArgs += ["-h", "${buildDir}/target/headers"]
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task cargoReleaseBuild(type: Exec) {
    workingDir '../datafusion-jni'
    executable "${System.getProperty('user.home')}/.cargo/bin/cargo"
    args += ['build', '--release']
}

PublishArtifact jniArtifact = null

def dylibFile = layout.buildDirectory.file("../../datafusion-jni/target/release/libdatafusion_jni.dylib")
if (dylibFile.get().asFile.exists()) {
    jniArtifact = artifacts.add('archives', dylibFile.get().asFile) {
        type = 'dylib'
        extension = 'dylib'
        classifier = osdetector.classifier
        builtBy 'cargoReleaseBuild'
    }
}

def soFile = layout.buildDirectory.file("../../datafusion-jni/target/release/libdatafusion_jni.so")
if (soFile.get().asFile.exists()) {
    jniArtifact = artifacts.add('archives', dylibFile.get().asFile) {
        type = 'so'
        extension = 'so'
        classifier = osdetector.classifier
        builtBy 'cargoReleaseBuild'
    }
}

publishing {
    publications {
        gpr(MavenPublication) {
            artifactId = 'datafusion-java'
            from(components.java)
            artifact jniArtifact
            pom {
                name = 'DataFusion Java'
                description = 'A Java binding to Apache Arrow DataFusion library'
                url = 'https://github.com/datafusion-contrib/datafusion-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/datafusion-contrib/datafusion-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.0'
    implementation 'org.apache.arrow:arrow-format:7.0.0'
    implementation 'org.apache.arrow:arrow-vector:7.0.0'
    runtimeOnly 'org.apache.arrow:arrow-memory-unsafe:7.0.0'
}

