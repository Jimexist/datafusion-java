plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'datafusion.java-conventions'
    id 'com.diffplug.spotless'
    id 'com.google.osdetector'
}

dependencies {
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.17.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.17.0'
    implementation 'org.apache.arrow:arrow-format:7.0.0'
    implementation 'org.apache.arrow:arrow-vector:7.0.0'
    runtimeOnly 'org.apache.arrow:arrow-memory-unsafe:7.0.0'
}

spotless {
    java {
        googleJavaFormat()
    }
}

java {
    withJavadocJar()
    withSourcesJar()

    compileJava {
        options.compilerArgs += ["-h", "${buildDir}/target/headers"]
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

task cargoReleaseBuild(type: Exec) {
    workingDir "$rootDir/datafusion-jni"
    executable "${System.getProperty('user.home')}/.cargo/bin/cargo"
    args += ['build', '--release']
}

def extensionMapping = [
        "osx"    : "dylib",
        "linux"  : "so",
        "windows": "dll"
]

task copyBuiltLibrary(type: Copy) {
    def extension = extensionMapping[osdetector.os]
    from "${rootDir}/datafusion-jni/target/release/libdatafusion_jni.$extension"
    into layout.buildDirectory.dir("jni_libs/${osdetector.classifier}")
    dependsOn cargoReleaseBuild
}

def classifierOsx = 'osx-x86_64'
def extensionOsx = 'dylib'
def jniLibOsx = layout.buildDirectory.file("jni_libs/$classifierOsx/libdatafusion_jni.$extensionOsx")

def classifierLinux = 'linux-x86_64'
def extensionLinux = 'so'
def jniLibLinux = layout.buildDirectory.file("jni_libs/$classifierLinux/libdatafusion_jni.$extensionLinux")

task jarWithOsxLib(type: Jar) {
    from sourceSets.main.output
    from jniLibOsx
    rename "libdatafusion_jni.$extensionOsx", "jni_libs/libdatafusion_jni.$extensionOsx"
    archiveClassifier.set(classifierOsx)
    dependsOn copyBuiltLibrary
}

task jarWithLinuxLib(type: Jar) {
    from sourceSets.main.output
    from jniLibLinux
    rename "libdatafusion_jni.$extensionLinux", "jni_libs/libdatafusion_jni.$extensionLinux"
    archiveClassifier.set(classifierLinux)
    dependsOn copyBuiltLibrary
}

task jarWithLib(type: Jar) {
    from sourceSets.main.output
    from jniLibOsx
    rename "libdatafusion_jni.$extensionOsx", "jni_libs/libdatafusion_jni.$extensionOsx"
    from jniLibLinux
    rename "libdatafusion_jni.$extensionLinux", "jni_libs/libdatafusion_jni.$extensionLinux"
    dependsOn copyBuiltLibrary
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'datafusion-java'
            from(components.java)
            artifact jarWithLib
            pom {
                name = 'DataFusion Java'
                description = 'A Java binding to Apache Arrow DataFusion library'
                url = 'https://github.com/datafusion-contrib/datafusion-java'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'dev'
                        name = 'Apache Arrow Developers'
                        email = 'dev@arrow.apache.org'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:datafusion-contrib/datafusion-java.git'
                    developerConnection = 'scm:git:https://github.com/datafusion-contrib/datafusion-java.git'
                    url = 'https://github.com/datafusion-contrib/datafusion-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "Sonatype"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = findProperty('ossrhUsername') ?: System.getenv("MAVEN_USERNAME")
                password = findProperty('ossrhPassword') ?: System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

def artifacts = publishing.publications.mavenJava.artifacts

if (jniLibLinux.get().asFile.exists()) {
    artifacts.artifact([
            source    : jniLibLinux,
            classifier: classifierLinux,
            extension : extensionLinux
    ])
}

if (jniLibOsx.get().asFile.exists()) {
    artifacts.artifact([
            source    : jniLibOsx,
            classifier: classifierOsx,
            extension : extensionOsx
    ])
}


signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("publish") }
    def signingKeyId = findProperty("signingKeyId")
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
